apply plugin: 'com.android.application'
apply plugin: "android-apt"
apply plugin: "me.tatarka.retrolambda"

// Constants
def packageName = "com.daniel.sunshine"

// Build Script Dependencies
buildscript {
  repositories {
    mavenCentral()
  }

  dependencies {
    // android-apt
    classpath "com.neenbedankt.gradle.plugins:android-apt:1.4"

    // retrolambda
    classpath "me.tatarka:gradle-retrolambda:3.1.0"
  }
}

// Android Annotation Processor
apt {
  arguments {
    androidManifestFile variant.outputs[0].processResources.manifestFile
    resourcePackageName packageName
  }
}

// Android Plugin Configuration
android {
  compileSdkVersion 22
  buildToolsVersion "23.0.0 rc2"

  defaultConfig {
    applicationId packageName
    minSdkVersion 16
    targetSdkVersion 22
    versionCode 1
    versionName "1.0"
  }

  buildTypes {
    release {
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
    }
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }
}

// Module Dependencies
repositories {
  mavenCentral()

  // ActiveAndroid
  maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
}

dependencies {
  compile fileTree(dir: 'libs', include: ['*.jar'])
  compile 'com.android.support:appcompat-v7:22.2.0'
  compile 'com.android.support:gridlayout-v7:22.2.0'

  // Android Annotations
  apt "org.androidannotations:androidannotations:3.3.1"
  compile "org.androidannotations:androidannotations-api:3.3.1"

  // Guava
  compile "com.google.guava:guava:18.0"

  // ActiveAndroid
  compile "com.michaelpardo:activeandroid:3.1.0-SNAPSHOT"

  // Retrofit
  compile "com.squareup.retrofit:retrofit:1.9.0"

  // OkHttp
  compile 'com.squareup.okhttp:okhttp-urlconnection:2.0.0'
  compile 'com.squareup.okhttp:okhttp:2.0.0'

  // Joda-Time
  compile "joda-time:joda-time:2.8"

  // RxJava
  compile 'io.reactivex:rxjava:1.0.10'

  // Picasso
  compile "com.squareup.picasso:picasso:2.5.2"
}

// Custom Hooks for android plugin tasks.
gradle.projectsEvaluated {
  compileDebugSources.doLast {
    "/usr/local/bin/growlnotify -t Android Studio -m Compiled".execute()
  }

  assembleDebug.doLast {
    "/usr/local/bin/growlnotify -t Android Studio -m Assembled".execute()
  }
}
