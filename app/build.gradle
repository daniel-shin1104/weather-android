apply plugin: 'com.android.application'
apply plugin: "android-apt"

// Constants
def packageName = "com.daniel.sunshine"
def AndroidAnnotationsVersion = "3.3.1"

// Build Script Dependencies
buildscript {
  repositories {
    mavenCentral()
  }

  dependencies {
    classpath "com.neenbedankt.gradle.plugins:android-apt:1.4"
  }
}

// Android Annotation Processor
apt {
  arguments {
    androidManifestFile variant.outputs[0].processResources.manifestFile
    resourcePackageName packageName
  }
}

// Android Plugin Configuration
android {
  compileSdkVersion 22
  buildToolsVersion "23.0.0 rc2"

  defaultConfig {
    applicationId packageName
    minSdkVersion 16
    targetSdkVersion 22
    versionCode 1
    versionName "1.0"
  }

  buildTypes {
    release {
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
    }
  }
}

// Module Dependencies
repositories {
  mavenCentral()

  // ActiveAndroid
  maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
}

dependencies {
  compile fileTree(dir: 'libs', include: ['*.jar'])
  compile 'com.android.support:appcompat-v7:22.2.0'

  // Android Annotations
  apt "org.androidannotations:androidannotations:$AndroidAnnotationsVersion"
  compile "org.androidannotations:androidannotations-api:$AndroidAnnotationsVersion"

  // Guava
  compile "com.google.guava:guava:18.0"

  // ActiveAndroid
  compile "com.michaelpardo:activeandroid:3.1.0-SNAPSHOT"

  // Retrofit
  compile "com.squareup.retrofit:retrofit:1.9.0"

  // OkHttp
  compile 'com.squareup.okhttp:okhttp-urlconnection:2.0.0'
  compile 'com.squareup.okhttp:okhttp:2.0.0'
}

// Custom Hooks for android plugin tasks.
gradle.projectsEvaluated {
  compileDebugSources.doLast {
    "/usr/local/bin/growlnotify -t Android Studio -m Compiled".execute()
  }

  assembleDebug.doLast {
    "/usr/local/bin/growlnotify -t Android Studio -m Assembled".execute()
  }
}
